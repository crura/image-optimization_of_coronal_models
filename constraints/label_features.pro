pro label_features,img_enh,xs,ys,keepers,labelxs,labelys, $
       labeltext

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;                                                            ;;
;; Purpose: Allows user to locate points at which to label    ;;
;;            each feature in the provided constraint set.    ;;
;;            Used by filter_constraints.pro                  ;;
;;                                                            ;;
;; Inputs: img_enh - enhanced image from one of Vadim's save  ;;
;;           files                                            ;;
;;         xs,ys - 5 x n arry of x,y coordinates for each of  ;;
;;           the n identified features                        ;;
;;         keepers - n-element array specifying whether any   ;;
;;           features or not really of interest; generated by ;;
;;           filter_constraints.pro; a value of 1 indicates   ;;
;;           the corresponding feature should be plotted for  ;;
;;           labeling - otherwise the feature is ignored      ;;
;;                                                            ;;
;; Returns: labelxs,labelys - data coordinates of point at    ;;
;;            which label should be printed over the active   ;;
;;            plot window                                     ;;
;;          labeltext - set to the array index corresponding  ;;
;;            to the feature being plotted                    ;;
;;                                                            ;;
;; Keywords: none yet                                         ;;
;;                                                            ;;
;; Dependencies: none yet                                     ;;
;;                                                            ;;
;; Created: 02/16/18                                          ;;
;;                                                            ;;
;; Created by: Shaela Jones                                   ;;
;;                                                            ;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

; parameters
winsize=[700,700]


; initializations
szimg_enh=SIZE(img_enh)
szxs=SIZE(xs)
nfeatures=szxs[2]
if N_ELEMENTS(keepers) eq 0 then keepers=INTARR(nfeatures)+1
foo=WHERE(keepers eq 1,keepercnt)
labelxs=FLTARR(keepercnt)
labelys=FLTARR(keepercnt)
labeltext=STRARR(keepercnt)



; draw each feature and ask for label location
window,xsize=winsize[0],ysize=winsize[1],retain=2
cnt=0
for i=0,nfeatures-1 do begin
  if keepers[i] eq 1 then begin
    plot_image,alog(img_enh),background=255,color=0
    oplot,xs[*,i],ys[*,i],psym=2
    print,'Click on location to draw label'
    cursor,xlabel,ylabel,/up
    labelxs[cnt]=xlabel
    labelys[cnt]=ylabel
    labeltext[cnt]=STRCOMPRESS(i,/remove_all)
    cnt++
  endif
endfor


end
